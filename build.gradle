/*
 * Copyright 2020-2024 Cyface GmbH
 *
 * This file is part of the Cyface Serialization.
 *
 *  The Cyface Serialization is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  The Cyface Serialization is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with the Cyface Serialization.  If not, see <http://www.gnu.org/licenses/>.
 */
/**
 * The build gradle file for the Cyface Serialization.
 *
 * @author Armin Schnabel
 */
buildscript {
  ext.kotlin_version = "1.7.10"

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:4.7.5"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id 'eclipse'
  id 'idea'
  id "com.github.spotbugs" version "4.7.5" apply false
  // Plugin to display the Gradle task graph
  //noinspection SpellCheckingInspection
  id 'org.barfuin.gradle.taskinfo' version '2.1.0'
  // protobuf plugin 0.9.0-2 fail: https://github.com/google/protobuf-gradle-plugin/issues/686
  id 'com.google.protobuf' version '0.8.19' apply false // Maybe keep in sync with `plugins` in `protos/build.gradle
}

allprojects {
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'maven-publish'
  apply plugin: 'com.github.spotbugs'
  apply plugin: 'jacoco'
  apply plugin: 'checkstyle'
  apply plugin: 'pmd'
  apply plugin: 'java'
  apply plugin: 'kotlin'

  group = 'de.cyface'
  version = "0.0.0" // Automatically overwritten by CI

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    checkstyle {
      toolVersion = '8.31'
      // use one common config file for all subprojects
      configFile = project(':').file("config/checkstyle/checkstyle.xml")
      //noinspection SpellCheckingInspection
      configProperties = ["suppressionFile": project(':').file("config/checkstyle/suppressions.xml")]
      ignoreFailures = true
      showViolations = true
    }
  }

  ext {
    slf4jVersion = '1.7.36' // major version available but breaks `backend.LocationMatchOperatorTest`
    commonsLangVersion = '3.12.0'
    sqliteDriverVersion = '3.40.1.0'
    gradleWrapperVersion = '7.6.1'
    protobufVersion = '3.22.2' // Keep in sync with protobufVersion in `protos/build.gradle`
    cyfaceProtosVersion = '2.2.0'

    // Versions of testing dependencies
    junitVersion = '5.9.2'
    mockitoVersion = '5.2.0'
    hamcrestVersion = '2.2'

    jacocoVersion = '0.8.9'
    spotBugsPluginVersion = '1.12.0'
    spotbugsToolsVersion = '4.7.3'
    pmdToolsVersion = '6.55.0'
  }

  wrapper {
    gradleVersion = "$gradleWrapperVersion"
  }

  compileKotlin {
    kotlinOptions {
      jvmTarget = "11"
    }
  }
  compileTestKotlin {
    kotlinOptions {
      jvmTarget = "11"
    }
  }

  dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version" // there is no stdlib-jdk11

    // Testing Dependencies
    testImplementation(platform("org.junit:junit-bom:$junitVersion"))
    testImplementation "org.junit.jupiter:junit-jupiter-params"  // Required for parameterized tests
    testImplementation "org.hamcrest:hamcrest:$hamcrestVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:$spotBugsPluginVersion"
  }

  test {
    useJUnitPlatform()
    testLogging {
      events "passed", "skipped", "failed"

      // Also show assert message (e.g. on the CI) when tests fail to identify cause
      showExceptions true
      exceptionFormat "full"
      showCauses false // not working with vertx fail() so we disable this
      showStackTraces false
      showStandardStreams = false
    }
  }

  jacoco {
    toolVersion = "$jacocoVersion"
    reportsDir = file("$buildDir/reports/jacoco")
  }

  jacocoTestReport {
    reports {
      xml.enabled true
      csv.enabled true
      html.destination file("${buildDir}/reports/jacocoHtml")
    }
  }

  spotbugs {
    toolVersion = "$spotbugsToolsVersion"
    ignoreFailures = true
    excludeFilter = file("$rootProject.projectDir/config/spotbugs/excludeFilter.xml")
  }

  // After hours of trying to fix local `./gradlew check` execution only this helped [DAT-803]
  // (https://github.com/spotbugs/spotbugs-gradle-plugin/issues/32#issuecomment-779661275)
  gradle.taskGraph.beforeTask { task ->
    if (task.name.toLowerCase().contains('spotbugs')) {
      task.reports {
        xml.enabled = false
        html.enabled = true
      }
    }
  }

  pmd {
    toolVersion = "$pmdToolsVersion"
    incrementalAnalysis = true
    ruleSetFiles = project(':').files('config/pmd.xml')
    rulesMinimumPriority = 4
    ruleSets = []
    // There are so many violations and currently it is not really important in this application.
    ignoreFailures = true
  }

  // Definitions for the maven-publish Plugin
  publishing {
    // The following repositories are used to publish artifacts to.
    repositories {
      maven {
        name = 'github'
        url = uri("https://maven.pkg.github.com/cyface-de/serialization")
        credentials {
          username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
          password = project.findProperty("gpr.key") ?: System.getenv("PASSWORD")
        }
      }
      maven {
        name = 'local'
        url = "file://${rootProject.buildDir}/repo"
      }
    }
  }
}
